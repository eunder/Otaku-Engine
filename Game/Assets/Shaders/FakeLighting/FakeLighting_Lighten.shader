Shader "Custom/FakeLighting_Lighten"
{
    Properties
    {
        _Color("Color",Color) = (1,1,1,0.5)       
    }   
    SubShader
    {
        Tags
        {
            "RenderType" = "Transparent"
            "Queue" = "Transparent"
        }    
        CGINCLUDE
        #include "UnityCG.cginc"

        float4 _Color;
        struct appdata
        {
            float4 vertex : POSITION;
        };
        
        struct v2f
        {
            float4 vertex : SV_POSITION;
        };

        v2f vert(appdata v)
        {
                 v2f o;
                o.vertex = UnityObjectToClipPos(v.vertex);
                o.vertex.z -= 0.00001 * o.vertex.w; //this is what prevents z fighting

                 return o;
        }
        
        fixed4 frag(v2f i) : SV_Target
        {
            return _Color * (_Color.a * 2);  // * 2 so tha the max brightness is higher. And also so that the game does not need to require a "luminance" field
        }

        ENDCG       
        Pass
        {
            Name "Mask"
            Ztest Greater
            Zwrite Off
            Cull Front
            Colormask 0
            
            Stencil
            {
                Ref 1
                Comp Greater              
                Pass Replace
            }

            //this part is needed or else exanding the block will yield no results
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            ENDCG
        }
        
        Pass
        {
            Name "Light Outside"
            Zwrite Off
            Ztest Lequal
            Cull Back
            Blend DstColor One
            
            Stencil
            {
                Comp Equal
                Ref 1
                Pass Zero
                Fail Zero
                Zfail Zero
            }
            
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            
            ENDCG
        }
        
        Pass
        {
            Name "Light Inside"
            ZTest Off
            ZWrite Off
            Cull Front
            Blend DstColor One
            
            Stencil
            {
                Ref 1
                Comp Equal
                Pass Zero
            }
            
            CGPROGRAM
            #pragma vertex vert
            #pragma fragment frag
            
            ENDCG
        }
    }
}